apply plugin: 'java'
apply plugin: 'maven'
apply from: "intellij.gradle"
apply plugin: 'spring-boot'
apply plugin: 'groovy'
apply plugin: 'signing'

version = '0.7.1.2'
group = 'com.bericotech'


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.5.RELEASE'
    }
}


repositories {
    mavenCentral()
    maven {
        url = "http://maven.springframework.org/release"
    }
    maven {
        url = "http://maven.springframework.org/milestone"
    }
    maven { url 'https://repo.spring.io/libs-snapshot'}
}


dependencies {

    ext.dropwizardversion = '0.7.1'
    ext.springIoPlatfromVersion = '1.0.1.RELEASE'


    versionManagement ("io.spring.platform:platform-versions:${ext.springIoPlatfromVersion}@properties")

    compile("io.dropwizard:dropwizard-core:${ext.dropwizardversion}")
    compile("io.dropwizard:dropwizard-auth:${ext.dropwizardversion}")
    compile("io.dropwizard:dropwizard-configuration:${ext.dropwizardversion}")

    compile 'org.springframework:spring-core'
    compile 'org.springframework.security:spring-security-core'

    compile'org.springframework.security:spring-security-web'
    compile'org.springframework.security:spring-security-config'
    compile'org.springframework:spring-beans'
    compile'org.springframework:spring-context'


    testCompile("junit:junit:4.10")
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}


artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}


signing {
    sign configurations.archives
}


uploadArchives {
    def berico_nexusPassword = project.hasProperty('berico_nexusPassword') ? "$berico_nexusPassword" : ''
    def berico_nexusUsername = project.hasProperty('berico_nexusUsername') ? "$berico_nexusUsername" : ''
    repositories {
        mavenDeployer {

            // see: http://yennicktrevels.com/blog/2013/10/11/automated-gradle-project-deployment-to-sonatype-oss-repository/
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // see: https://github.com/Berico-Technologies/Fallwizard/issues/15
            // see: http://stackoverflow.com/questions/23665976/unable-to-publish-to-local-maven-repo-when-using-gradle-and-spring-boot
            pom.project {
                parent {
                    groupId 'org.springframework.boot'
                    artifactId 'spring-boot-starter-parent'
                    version '1.1.5.RELEASE'
                }
                name 'Fallwizard'
                packaging 'jar'
                description 'Allows dropwizard users to use the Spring Framework'
                url 'https://github.com/Berico-Technologies/Fallwizard'
                scm {
                    url 'scm:git@github.com:Berico-Technologies/Fallwizard.git'
                    connection 'scm:git@github.com:Berico-Technologies/Fallwizard.git'
                    developerConnection 'scm:git@github.com:Berico-Technologies/Fallwizard.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'berico-rclayton'
                        name 'Richard Clayton'
                    }
                    developer {
                        id 'jar349'
                        name 'John Ruiz'
                    }
                }
            }
            // when we're ready to publish to Maven Central, use this repository instead
            // (make sure a project has been created for us first!)
            // see: http://central.sonatype.org/pages/ossrh-guide.html#create-a-ticket-with-sonatype
            //
            //repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            //    authentication(userName: sonatypeUsername, password: sonatypePassword)
            //}
            repository(url: "http://nexus.bericotechnologies.com/content/repositories/releases/") {
                authentication(userName: berico_nexusUsername, password: berico_nexusPassword)
            }
            snapshotRepository(url: "http://nexus.berico.us/content/repositories/snapshots/") {
                authentication(userName: berico_nexusUsername, password: berico_nexusPassword)
            }
        }
    }
}